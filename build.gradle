buildscript {
    ext.kotlin_version = "1.1.4-2"
    ext.kotlin_coroutines_version = "0.18"
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "java"
    id "maven"
    id "maven-publish"
    id "com.jfrog.bintray" version "1.7.2"
    id "com.github.johnrengelman.shadow" version "1.2.4"
}

apply plugin: "kotlin"

group = "com.serebit"
version = "0.0.3"

description = """"""

repositories {
    jcenter()
    mavenCentral()
    maven { url "https://jitpack.io" }
}

dependencies {
    compile "com.sedmelluq:lavaplayer:1.2.36"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    compile "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlin_coroutines_version"
    compile "com.serebit:autotitan-api:0.1.0"
    compile "commons-validator:commons-validator:1.6"
    compile "net.dv8tion:JDA:3.0.0_200"
    compile "com.google.code.gson:gson:2.8.0"
    compile "khttp:khttp:0.1.0"
    compile "com.google.guava:guava:21.0"
    compile "org.slf4j:slf4j-simple:1.8.0-alpha2"
    compile "org.apache.logging.log4j:log4j-core:2.8.2"
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
}

kotlin {
    experimental.coroutines "enable"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

shadowJar {
    manifest.attributes "Main-Class": "com.serebit.autotitan.MainKt"
    archiveName = "autotitan-${version}.${extension}"
}

build {
    dependsOn shadowJar
}

publishing {
    publications {
        BintrayRelease(MavenPublication) {
            from components.java
            groupId group
            artifactId archivesBaseName
            version version
        }
    }
}

bintray {
    user = System.getenv("BINTRAY_USER")
    key = System.getenv("BINTRAY_KEY")
    publications = ["BintrayRelease"]
    pkg {
        repo = "Maven"
        name = "autotitan"
        licenses = ["Apache-2.0"]
        vcsUrl = "https://github.com/serebit/autotitan.git"
        publish = true
        version {
            name = project.version
            released = new Date()
        }
    }
}

bintrayUpload {
    dependsOn build

    onlyIf { !System.getenv("BINTRAY_USER").isEmpty() }
    onlyIf { !System.getenv("BINTRAY_KEY").isEmpty() }
}
